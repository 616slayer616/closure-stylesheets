plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "3.4.0.2513"
}

apply from: 'gradle/sonarqube.gradle'

group = 'org.padler'
version = '1.6.5'
description = 'Closure Stylesheets'
java.sourceCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("args4j:args4j:2.33")
    implementation("com.google.code.gson:gson:2.9.1")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("com.google.javascript:closure-compiler-unshaded:v20221004")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.1")
    testImplementation('org.mockito:mockito-core:4.8.0')
    testImplementation('org.mockito:mockito-junit-jupiter:4.8.0')
    testImplementation("com.google.truth:truth:0.45")
    testImplementation('org.assertj:assertj-core:3.23.1')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'closure-stylesheets'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Closure-stylesheets'
                description = 'Closure Stylesheets is an extension to CSS that adds variables, functions,\n' +
                        'conditionals, and mixins to standard CSS. The tool also supports\n' +
                        'minification, linting, RTL flipping, and CSS class renaming.'
                url = 'https://github.com/616slayer616/natorderr'
                licenses {
                    license {
                        name = 'License'
                        url = 'https://github.com/616slayer616/closure-stylesheets/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = '616slayer616'
                        name = 'Patrick Adler'
                        email = 'patrick@padler.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/616slayer616/closure-stylesheets.git'
                    developerConnection = 'scm:git:ssh://github.com/616slayer616/closure-stylesheets.git'
                    url = 'https://github.com/616slayer616/closure-stylesheetss'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/616slayer616/closure-stylesheets"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
